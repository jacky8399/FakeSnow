plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java-library'
    id 'idea'
    id 'io.papermc.paperweight.userdev' version "1.3.8"
}

group = 'com.jacky8399'
version = '1.3.1.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://papermc.io/repo/repository/maven-public/")
    }
    maven {
        name = 'ProtocolLib'
        url "https://repo.dmulloy2.net/nexus/repository/public/"
    }
    maven {
        name = 'WorldGuard'
        url "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.19.2-R0.1-SNAPSHOT")
    compileOnly "com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT"
    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.0.6')
    implementation("org.bstats:bstats-bukkit:3.0.0")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate "org.bstats", "com.jacky8399.fakesnow.bstats"
}

tasks.assemble.dependsOn reobfJar