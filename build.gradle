import java.nio.charset.StandardCharsets

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java-library'
    id 'io.papermc.paperweight.userdev' version "1.4.0" apply false
}

group = 'com.jacky8399'
version = '1.3.1.1'

compileJava {
    options.release.set(17)
    options.encoding = StandardCharsets.UTF_8.name()
}

allprojects {
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            content {
                includeGroup 'org.spigotmc'
            }
        }
        maven {
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = 'ProtocolLib'
            url = "https://repo.dmulloy2.net/nexus/repository/public/"

            content {
                includeGroup 'com.comphenix.protocol'
            }
        }
        maven {
            name = 'WorldGuard'
            url = "https://maven.enginehub.org/repo/"
        }
    }
}

dependencies {
    implementation(project(":common"))

    implementation(project(path: ":v1_19_R1", configuration: 'reobf'))
    implementation(project(path: ":v1_19_R2", configuration: 'reobf'))

    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.6")
    implementation("org.bstats:bstats-bukkit:3.0.0")
}

// replace plugin.yml version with Gradle build version
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = StandardCharsets.UTF_8.name()
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    assemble {
        dependsOn shadowJar
    }
}

shadowJar {
    archiveClassifier.set('')

    relocate "org.bstats", "com.jacky8399.fakesnow.bstats"
}